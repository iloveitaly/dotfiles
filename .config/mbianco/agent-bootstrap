# Should be loaded very soon in zshrc
# Set AGENT_DEBUG=1 to enable debug mode

# shellcheck source=./.path
source "${HOME}/.config/mbianco/path"

# Returns 0 (true) when running under an AI-agent managed terminal.
ai_is_agent_terminal() {
  [[ "${VSCODE_AGENT:-}" == "1" || -n "${CURSOR_AGENT:-}" || "${CLAUDECODE:-}" == "1" || "${GEMINI_CLI:-}" == "1" ]]
}

# test with:
# env -i AGENT_DEBUG=1 VSCODE_AGENT=1 USER=$(whoami) HOME="$HOME" $(which zsh) -l
ai_load_agent_configuration() {
  # this is loaded when `TERM=dumb` is set, which is the case in the cursor agent terminal (and any non-interactive terminal)
  # however, it's also loaded when running `zsh` in tmux, so we need to be careful about what we put here.

  # Return early if not running in a dumb terminal, this is what cursor specifies
  # if [[ "${TERM:-}" != "dumb" && -o interactive ]]; then
  #   [[ ${AGENT_DEBUG:-} -eq 1 ]] && echo "Interactive shell detected"
  #   return 0
  # fi

  # Warn if shell nesting is excessive
  local shlvl="${SHLVL:-0}"
  if [[ "$shlvl" -gt 5 ]]; then
    echo -e "\033[33mWarning: Excessive shell nesting detected (SHLVL=$shlvl)\033[0m"
  fi

  # Debug: Log if shell level is greater than 1
  if [[ "$shlvl" -gt 1 && ${AGENT_DEBUG:-} -eq 1 ]]; then
    echo -e "\033[36mDebug: Shell nesting level is $shlvl\033[0m"
  fi

  # VS Code does not operate the same as Cursor: terminals are interactive, not dumb, and no ENV var is set to indicate
  # it's being managed by an agent. We manually set a variable in our VCS config to indicate this.
  if ai_is_agent_terminal; then
    # mise first, since it manages/install direnv
    source <(mise activate)
    source <(direnv hook zsh)

    # Only run VS Code shell integration for VSC and Cursor
    # https://code.visualstudio.com/docs/terminal/shell-integration
    if [[ "${VSCODE_AGENT:-}" == "1" || -n "${CURSOR_AGENT:-}" ]]; then
      source "$(code --locate-shell-integration-path zsh)"
    fi

    [[ ${AGENT_DEBUG:-} -eq 1 ]] && echo "In agent shell, sourced configuration"

    export AGENT_CONFIGURED=1

    # without this, the git config which uses fancy pagers, etc will be used
    export GIT_CONFIG_NOSYSTEM=1
    export GIT_CONFIG_GLOBAL=/dev/null

    return 0
  else
    echo -e "\033[33mNot in agent, and not interactive. We may be missing a new agent type?\033[0m"
    return 1
  fi
}